## Class for recipes needing qmake (qt5.x)
##
## Is used by recipes which needs qmake. it sets up the correct
## paths, needed for generating makefiles using qmake, and aftwards
## needed variables for compilation.

inherit qmake5_base
inherit pkgconfig-install

QT5TOOLSDEPENDS ?= "native:qmake5_${PV}"
CLASS_DEPENDS += "${QT5TOOLSDEPENDS} "

addtask generateqtconfigfile after do_patch before do_configure

do_generateqtconfigfile() {
    cat > ${QT_CONF_PATH} <<EOF
[Paths]
Prefix = ${OE_QMAKE_PATH_PREFIX}
Headers = ${OE_QMAKE_PATH_HEADERS}
Libraries = ${OE_QMAKE_PATH_LIBS}
ArchData = ${OE_QMAKE_PATH_ARCHDATA}
Data = ${OE_QMAKE_PATH_DATA}
Binaries = ${OE_QMAKE_PATH_BINS}
LibraryExecutables = ${OE_QMAKE_PATH_LIBEXECS}
Plugins = ${OE_QMAKE_PATH_PLUGINS}
Imports = ${OE_QMAKE_PATH_IMPORTS}
Qml2Imports = ${OE_QMAKE_PATH_QML}
Translations = ${OE_QMAKE_PATH_TRANSLATIONS}
Documentation = ${OE_QMAKE_PATH_DOCS}
Settings = ${OE_QMAKE_PATH_SETTINGS}
Examples = ${OE_QMAKE_PATH_EXAMPLES}
Tests = ${OE_QMAKE_PATH_TESTS}
HostBinaries = ${OE_QMAKE_PATH_HOST_BINS}
HostData = ${OE_QMAKE_PATH_HOST_DATA}
HostSpec = ${OE_QMAKESPEC}
TartgetSpec = ${OE_XQMAKESPEC}
Sysroot = ${STAGE_DIR}/machine/
EOF
}

do_configure_qmake5_base () {
    ${OE_QMAKE_QMAKE} "`ls ${S}/*.pro`"
}

do_install_qmake5_base() {
   # Fix install paths for all
    find -name "Makefile*" | xargs sed -i "s,(INSTALL_ROOT)${STAGE_DIR}/sysroot/,(INSTALL_ROOT),g"
    find -name "Makefile*" | xargs sed -i "s,(INSTALL_ROOT)${STAGE_DIR}/machine/,(INSTALL_ROOT),g"

    oe_runmake install INSTALL_ROOT=${D}/
}

do_configure() {
    do_configure_qmake5_base
}

do_install() {
    do_install_qmake5_base
}
