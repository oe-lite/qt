#
# QMake variables for Qt4
#
CLASS_DEPENDS += "native:qmake_${PV}"
export QMAKESPEC = "${TARGET_SYSROOT}${target_datadir}/${OE_QMAKE_LIBS_QT}/mkspecs/${BUILD_CXX}"
QMAKESPEC:native = ""
export OE_QMAKE_UIC = "uic"
export OE_QMAKE_MOC = "moc"
export OE_QMAKE_RCC = "rcc"
export OE_QMAKE_QDBUSCPP2XML = "qdbuscpp2xml"
export OE_QMAKE_QDBUSXML2CPP = "qdbusxml2cpp"
export OE_QMAKE_LRELEASE = "lrelease"
export OE_QMAKE_LUPDATE = "lupdate"
export OE_QMAKE_QMAKE = "qmake"
export OE_QMAKE_CC="${CC}"
export OE_QMAKE_CFLAGS="${CFLAGS}"
export OE_QMAKE_CXX="${CXX}"
export OE_QMAKE_LINK = "${CXX}"
export OE_QMAKE_LDFLAGS="${LDFLAGS}"
export OE_QMAKE_AR="${AR}"
export OE_QMAKE_STRIP="echo"
export OE_QMAKE_CXXFLAGS = "${CXXFLAGS}"
export OE_QMAKE_LIBS_QT = "qt"
export OE_QMAKE_INCDIR_QT = "${HOST_SYSROOT}${includedir}/${OE_QMAKE_LIBS_QT}"
export OE_QMAKE_LIBDIR_QT = "${HOST_SYSROOT}${libdir}"
export OE_QMAKE_LIBS_QT = "qt"

# do not export STRIP to the environment
STRIP[unexport] = "1"

#
# qt.conf overwrite paths hardcoded in the qmake binary.
#
# The qmake paths is used for fx the install targets.
# 
# The Paths comnpiled into the libs (fx pluginpath) must be set, when running
# configure, use QT_PATH below to match the qmake paths.
#
QT_CONF_PATH = "${SRCDIR}/qt.conf"
export QT_CONF_PATH
do_configure[prefuncs] += "do_configure_conf"
do_configure_conf() {
    echo "[Paths]"                                 > $QT_CONF_PATH
    echo "Prefix=${prefix}/"                      >> $QT_CONF_PATH
    echo "Documentation=${docdir}/${OE_QMAKE_LIBS_QT}" >> $QT_CONF_PATH
    echo "Headers=${includedir}/${OE_QMAKE_LIBS_QT}"   >> $QT_CONF_PATH
    echo "Libraries=${libdir}"                    >> $QT_CONF_PATH
    echo "Binaries=${bindir}"                     >> $QT_CONF_PATH
    echo "Plugins=${libdir}/${OE_QMAKE_LIBS_QT}/plugins" >> $QT_CONF_PATH
    echo "Data=${datadir}/${OE_QMAKE_LIBS_QT}"         >> $QT_CONF_PATH
    echo "Translations=${datadir}/${OE_QMAKE_LIBS_QT}/translations" >> $QT_CONF_PATH
    echo "Settings=${sysconfdir}/${OE_QMAKE_LIBS_QT}"  >> $QT_CONF_PATH
    echo "Examples=${bindir}/${OE_QMAKE_LIBS_QT}/examples" >> $QT_CONF_PATH
    echo "Demos=${bindir}/${OE_QMAKE_LIBS_QT}/demos"   >> $QT_CONF_PATH
}


QT_PATH = " \
    -prefix ${prefix}/ \
    -bindir ${bindir} \
    -libdir ${libdir} \
    -datadir ${datadir}/${OE_QMAKE_LIBS_QT} \
    -sysconfdir ${sysconfdir}/${OE_QMAKE_LIBS_QT} \
    -docdir ${docdir}/${OE_QMAKE_LIBS_QT} \
    -headerdir ${includedir}/${OE_QMAKE_LIBS_QT} \
    -plugindir ${libdir}/${OE_QMAKE_LIBS_QT}/plugins \
"

do_configure_install_conf () {
    if [ -z "${OE_QMAKE_CXX}" ]; then
        echo "ERROR: cannot install $1 to $2, OE_QMAKE var not set"
        exit 1
    fi
    cp -vf "$1" "$2"
    sed -i -e 's#\$\((OE_QMAKE_CXX)\)#${OE_QMAKE_CXX}#' \
        -i -e 's#\$\((OE_QMAKE_CC)\)#${OE_QMAKE_CC}#' \
        -i -e 's#\$\((OE_QMAKE_LINK)\)#${OE_QMAKE_LINK}#' \
        -i -e 's#\$\((OE_QMAKE_LDFLAGS)\)#${OE_QMAKE_LDFLAGS}#' \
        -i -e 's#\$\((OE_QMAKE_CFLAGS)\)#${OE_QMAKE_CFLAGS}#' \
        -i -e 's#\$\((OE_QMAKE_CXXFLAGS)\)#${OE_QMAKE_CXXFLAGS}#' \
        "$2"
}
